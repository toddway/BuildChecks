buildscript {
    ext.kotlin_version = '1.3.21'
    ext.okHttpVersion = "3.9.0"
    ext.retrofitVersion = '2.4.0'
    repositories {
        jcenter()
        flatDir { dirs "libs" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
        classpath "de.aaschmid:gradle-cpd-plugin:2.0-SNAPSHOT"
        classpath "com.toddway:buildChecks:1.11"

        //these are here because they're dependencies of the buildChecks.jar file
        classpath 'io.reactivex.rxjava2:rxjava:2.1.14'
        classpath "com.squareup.retrofit2:retrofit:$retrofitVersion"
        classpath "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        classpath "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
        classpath "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
        classpath "com.jakewharton.picnic:picnic:0.3.0"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
}

apply from: 'gradle/publish.gradle'
apply from: 'gradle/jacocoJavaLibrary.gradle'
apply from: 'gradle/cpd.gradle'
apply from: 'gradle/detekt.gradle'
apply from: 'gradle/buildChecks.gradle'

task checks {
    dependsOn 'cpdCheck'
    dependsOn 'detekt'
    dependsOn 'testJacoco'
    finalizedBy 'printChecks'
}

task copyJar(type: Copy) {
    from "$buildDir/libs/buildChecks-${version}.jar"
    into "$projectDir/libs/"
    rename { "buildChecks.jar" }
}
build.dependsOn(copyJar)
publishPlugins.dependsOn(checks)

test {
    //ignoreFailures true
    testLogging {
        exceptionFormat = 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["passed", "skipped", "failed", "standardOut", "standardError"]
    }
}

apply plugin: 'kotlin'

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.jakewharton.picnic:picnic:0.3.0"

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.34'
}


