buildscript {
    ext.kotlin_version = '1.8.21'
    ext.okHttpVersion = "3.9.0"
    ext.retrofitVersion = '2.4.0'
    repositories {
        jcenter()
        flatDir { dirs "libs" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"

        classpath "com.toddway:buildchecks"
        //these are here only because they're dependencies of the buildChecks.jar
        classpath 'io.reactivex.rxjava2:rxjava:2.1.14'
        classpath "com.squareup.retrofit2:retrofit:$retrofitVersion"
        classpath "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        classpath "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
        classpath "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
        classpath "com.jakewharton.picnic:picnic:0.3.0"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.18.1"
    id 'de.aaschmid.cpd' version '3.2'
}

apply from: 'gradle/publish.gradle'
apply from: 'gradle/buildChecks.gradle'
apply from: 'gradle/jacoco.gradle'
apply from: 'gradle/cpd.gradle'
apply from: 'gradle/detekt.gradle'

task checks {
    dependsOn 'cpdCheck'
    dependsOn 'detekt'
    //dependsOn 'testCoverage'
    finalizedBy 'printChecks'
    dependsOn 'openChecks'
}

postChecks.dependsOn(checks)
postChecks.finalizedBy(pushArtifacts)

task copyJar(type: Copy) {
    from "$buildDir/libs/buildChecks-${version}.jar"
    into "$projectDir/libs/"
    rename { "buildChecks.jar" }
}
build.dependsOn(copyJar)
publishPlugins.dependsOn(checks)

test {
    testLogging.showStandardStreams = true
    //ignoreFailures true
    testLogging {
        exceptionFormat = 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["passed", "skipped", "failed", "standardOut", "standardError"]
    }
}

apply plugin: 'kotlin'

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation 'com.mitchtalmadge:ascii-data:1.4.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation "io.mockk:mockk:1.10.2"
}


